"secadm" (* slogan = "Manages security configuration and administration on network nodes" *) WS "cert" (* hide = "true" *) WS ( CERT_GET | CERT_ISSUE | CERT_REISSUE )

CERT_GET ::= "get" (* slogan = "Returns the status of the certificate enrollment." *) WS CERT_TYPE_OPTION TYPE_OF_CERT CERT_TARGET_NODES
CERT_ISSUE ::= "issue" (* slogan = "Starts a job to issue the certificates on the nodes" *) WS ( ISSUE_IPSEC_CERT_TYPE_OPTION | ISSUE_OAM_CERT_TYPE_OPTION )
CERT_REISSUE ::= "reissue" (* slogan = "Starts a job to reissue the certificates to the nodes" *) WS CERT_TYPE_OPTION TYPE_OF_CERT ( REISSUE_CERT_TYPE | REISSUE_CA_NAME )

REISSUE_CERT_TYPE ::= REISSUE_CERT_TARGET_NODES [ CERT_REASON ]
REISSUE_CA_NAME ::= WS CA_NAME ( CERT_SERIAL_NUMBER | REISSUE_CERT_TARGET_NODES ) [ CERT_REASON ]

CERT_TYPE_OPTION ::= ( "-ct" (* hide = "true" *) | "--certtype" (* slogan = "Specify the certificate type to issue e.g IPSEC, OAM" *) )
TYPE_OF_CERT ::= WS ( "IPSEC" (* slogan = "Internet Protocol security (ipsec) certificate type" *) | "OAM" (* slogan = "OAM certificate type" *) )

ISSUE_IPSEC_CERT_TYPE_OPTION ::= CERT_TYPE_OPTION WS "IPSEC" (* slogan = "Internet Protocol security (ipsec) certificate type" *) WS CERT_XML_FILE  [ EXTERNAL_CA ]
ISSUE_OAM_CERT_TYPE_OPTION ::= CERT_TYPE_OPTION WS "OAM" (* slogan = "OAM certificate type" *) WS CERT_XML_FILE

CERT_TARGET_NODES ::= WS ( CERT_NODE_LIST | CERT_NODE_FILE )
REISSUE_CERT_TARGET_NODES ::= WS ( CERT_NODE_LIST | CERT_NODE_FILE | SAVED_SEARCH | COLLECTION )
CERT_NODE_LIST ::= ( "-n" (* hide = "true" *) | "--nodelist" (* slogan = "Specify the list of nodes. Tip: -n for short" *) ) WS CERT_NODE_NAME { ( "," | ";" ) (* slogan = "Comma used to add another node name to the list." *) CERT_NODE_NAME }
CERT_NODE_NAME ::= ( <node_name> (* slogan = "Scope the Command using Node names. E.g. MyNode1,MyNode2" *) | "NetworkElement="  <node_name> (* slogan = "Scope the Command using Node names. E.g. MyNode1,MyNode2" *) )
CERT_NODE_FILE ::= ( "-nf" (* hide = "true" *) | "--nodefile" (* slogan = "Specify the file containing a list of nodes. Tip: -nf for short" *) ) WS "file:" <file_name> (* inputType = "fileName" slogan = "Specify a file containing a list of nodes" *)

CERT_XML_FILE ::= ( "-xf" (* hide = "true" *) | "--xmlfile" (* slogan = "Specify the xml file. Tip: -xf for short" *) ) WS "file:" <file_name> (* inputType = "fileName" slogan = "Specify an xml file" *)

CA_NAME ::= "-ca" (* slogan = "Specifies the ca name" *) WS <ca_name_value> (* slogan = "Defines the name of the issuer CA entity" *)

CERT_SERIAL_NUMBER ::= WS ( "-sn" (* hide = "true" *) | "--serialnumber" (* slogan = "Specifies the serial number associated to the certificate. Tip: -sn for short" *) ) WS ( <serial_number_value> (* slogan = "Specify a serial number" *) | "all" (* slogan = "All serial numbers associated to the certificate" *) )

CERT_REASON ::= WS ( "-r" (* hide = "true" *) | "--reason" (* slogan = "Specify a reason. Tip: -r for short" *) ) WS CERT_REASON_VALUE

CERT_REASON_VALUE ::= ( "unspecified" | "keyCompromise" | "CACompromise" | "affiliationChanged" | "superseded" | "cessationOfOperation" | "certificateHold" | "removeFromCRL" | "privilegeWithdrawn" | "AACompromise" )

SAVED_SEARCH ::= ( "-sa" (* hide = "true" *) | "--savedsearch" (* slogan = "Specify the savedsearch containing list of nodes. Tip: -sa for short" *) ) WS SAVED_SEARCH_NAME { "," (* slogan = "Comma used to add another savedsearch to the list." *) SAVED_SEARCH_NAME }

COLLECTION ::= ( "-co" (* hide = "true" *) | "--collection" (* slogan = "Specify the collection containing list of nodes. Tip: -co for short" *) ) WS COLLECTION_NAME { "," (* slogan = "Comma used to add another collection to the list." *) COLLECTION_NAME }

SAVED_SEARCH_NAME ::= <saved_search_name> (* slogan = "Scope the Command using savedsearch names. E.g. MySavedSearch1, MySavedSearch2" *) )

COLLECTION_NAME ::= <collection_name> (* slogan = "Scope the Command using collection names. E.g. MyCollection1, MyCollection2" *) )

EXTERNAL_CA ::= WS ( "-ec" (* hide = "true" *) | "--extca" )