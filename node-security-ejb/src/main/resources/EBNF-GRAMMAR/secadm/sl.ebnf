"secadm" (* slogan = "Manages security configuration and administration on network nodes" *) WS "sl" (* hide = "true" *) WS ( SL_GET | SL_SET )

SL_GET ::= "get" (* slogan = "Get security level" *) [ SL_LEVEL_FILTER ] WS SL_GET_TARGET_NODES

SL_SET ::=  "set" (* slogan = "Set security level" *) SL_LEVEL_FILTER WS SL_SET_TARGET_NODES

SL_LEVEL_FILTER ::= WS ( "--level" (* slogan = "Security level of node. Tip: -l for short" *) | "-l" (* hide = "true" *) ) WS ( "1" (* slogan = "Security level 1" *) | "2" (* slogan = "Security level 2" *) )

SL_GET_TARGET_NODES ::= ( SL_ALL | SL_NODE_LIST | SL_NODE_FILE )

SL_ALL ::= "*" (* slogan = "All nodes" *)

SL_NODE_LIST ::= ( "-n" (* hide = "true" *) | "--nodelist" (* slogan = "Specify the list of nodes. Tip: -n for short" *) ) WS SL_NODE_NAME { "," (* slogan = "Comma used to add another node name to the list." *) SL_NODE_NAME }

SL_NODE_NAME ::= [ ( "MeContext=" | "NetworkElement=" ) ] <node_name> (* slogan = "Scope the Command using Node names. E.g. MyNode1,MyNode2" *)

SL_NODE_FILE ::= ( "-nf" (* hide = "true" *) | "--nodefile" (* slogan = "Specify the file containing a list of nodes. Tip: -nf for short" *) ) WS "file:" <file_name> (* inputType = "fileName" slogan = "Specify a file containing a list of nodes" *)

SL_SET_TARGET_NODES ::= SL_XML_FILE

SL_XML_FILE ::= ( "-xf" (* hide = "true" *) | "--xmlfile" (* slogan = "Specify the xml file. Tip: -xf for short" *) ) WS "file:" <file_name> (* inputType = "fileName" slogan = "Specify an xml file" *)