"secadm" (* slogan = "Manages security configuration and administration on network nodes" *) WS "trust" (* slogan = "Distributes, removes and gets trust certificates" *) WS ( TRUST_DISTRIBUTE | TRUST_REMOVE | TRUST_GET )


TRUST_DISTRIBUTE ::= ( "distr" (* hide = "true" *) | "distribute" (* slogan = "Starts a job for trust distribution to the nodes. Tip: distr for short" *) ) WS ( TRUST_CERT_OPTION | TRUST_CATEGORY_OPTION | EXTCA_TRUST_OPTION )

TRUST_REMOVE ::= ( "rm" (* hide = "true" *) | "remove" (* slogan = "Starts a job for trust removal from the nodes. Tip: rm for short" *) ) WS ( TRUST_CERT_TYPE TRUST_TYPE | TRUST_CATEGORY_TYPE TRUST_TYPE_OPTION ) TRUST_ISSUER WS TRUST_SERIAL_NUMBER WS TRUST_REMOVE_TARGET_NODE

TRUST_GET ::= "get" (* slogan = "Returns the status of the trust certificate installation" *) WS ( TRUST_CERT_TYPE TRUST_TYPE | TRUST_CATEGORY_TYPE TRUST_TYPE_OPTION ) TRUST_GET_TARGET_NODES

TRUST_CATEGORY_OPTION ::= ( TRUST_CATEGORY | TRUST_CATEGORY_LAAD )

EXTCA_TRUST_OPTION ::= EXTCA_TRUST IPSEC_TRUST_TYPE TRUST_NODE_XML_FILE EXTERNAL_CA

TRUST_CATEGORY ::= TRUST_CATEGORY_TYPE TRUST_TYPE ( TRUST_CA_NAME [ TRUST_TARGET_NODES ] | TRUST_TARGET_NODES )

TRUST_CERT_OPTION ::= TRUST_CERT_TYPE TRUST_TYPE ( TRUST_CA_NAME [ TRUST_TARGET_NODES ]  | TRUST_TARGET_NODES )

TRUST_TYPE_OPTION ::= ( OAM_TRUST_TYPE | IPSEC_TRUST_TYPE | LAAD_TRUST_TYPE )

TRUST_CATEGORY_LAAD ::= TRUST_CATEGORY_TYPE LAAD_TRUST_TYPE [ TRUST_CA_NAME ] TRUST_TARGET_NODES

TRUST_DISTRIBUTE_CA_NAME ::= TRUST_CERT_TYPE WS TRUST_TYPE TRUST_CA_NAME [ TRUST_TARGET_NODES ]

EXTCA_TRUST ::= ( TRUST_CERT_TYPE | TRUST_CATEGORY_TYPE )

TRUST_CERT_TYPE ::= ( "-ct" (* hide = "true" *) | "--certtype" (* slogan = "Cert type to issue e.g IPSEC,OAM Tip:-ct for short" *) )

TRUST_CATEGORY_TYPE ::= ( "-tc" (* hide = "true" *) | "--trustcategory" (* slogan = "Trust category type e.g IPSEC,OAM & LAAD Tip:-tc for short" *) )

TRUST_ISSUER ::= ( TRUST_ISSUER_DN | TRUST_CA_NAME )

TRUST_ISSUER_DN ::= WS ( "-isdn" (* hide = "true" *) | "--issuer-dn" (* slogan = "Issuer dn. Tip -isdn for short" *) ) WS <issuer_dn_value> (* inputType = "wrapDoubleQuotes,wrapSingleQuotes" slogan = "Specify the issuer dn of trusted certificate in double or single quotes." *)

TRUST_CA_NAME ::= WS "-ca" (* slogan = "Specifies the ca name" *) WS <ca_name_value> (* slogan = "CA value" *)

TRUST_SERIAL_NUMBER ::= ( "-sn" (* hide = "true" *) | "--serialnumber" (* slogan = "Specifies the serial number associated to the certificate. Tip: -sn for short" *) ) WS <serial_number_value> (* slogan = "Specify a serial number." *)

TRUST_TYPE ::= ( OAM_TRUST_TYPE | IPSEC_TRUST_TYPE )

OAM_TRUST_TYPE ::= WS "OAM" (* slogan = "trust category type of OAM" *)

IPSEC_TRUST_TYPE ::= WS "IPSEC" (* slogan = "trust category / cert type type of IPSEC" *)

LAAD_TRUST_TYPE ::= WS "LAAD" (* slogan = "trust category type of LAAD" *)

TRUST_GET_TARGET_NODES ::= WS ( TRUST_NODE_LIST | TRUST_NODE_FILE )

TRUST_TARGET_NODES ::= WS ( TRUST_NODE_LIST | TRUST_NODE_FILE | SAVED_SEARCH | COLLECTION )

TRUST_REMOVE_TARGET_NODE ::= ( TRUST_REMOVE_NODE_LIST | TRUST_NODE_FILE | SAVED_SEARCH | COLLECTION )

TRUST_NODE_LIST ::= ( "-n" (* hide = "true" *) | "--nodelist" (* slogan = "List of nodes. Tip: -n for short" *) ) WS TRUST_NODE_NAME { ( "," | ";" ) (* slogan = "Comma used to add another node name to the list." *) TRUST_NODE_NAME }

TRUST_REMOVE_NODE_LIST ::= ( "-n" (* hide = "true" *) | "--nodelist" (* slogan = "Specify the list of nodes. Tip: -n for short" *) ) WS TRUST_NODE_NAMES_OR_ALL

TRUST_NODE_NAMES_OR_ALL ::= ( TRUST_NODE_NAMES | "all" (* slogan = "All nodes" *) )

TRUST_NODE_NAMES ::= TRUST_NODE_NAME { ( "," | ";" ) (* slogan = "Comma used to add another node name to the list." *) TRUST_NODE_NAME }

TRUST_NODE_NAME ::= <node_name> (* slogan = "Scope the Command using Node names. E.g. MyNode1,MyNode2" *)

TRUST_NODE_FILE ::=  ( "-nf" (* hide = "true" *) | "--nodefile" (* slogan = "Specify the file containing a list of nodes. Tip: -nf for short" *) ) WS "file:" <file_name> (* inputType = "fileName" slogan = "Specify a file containing a list of nodes" *)

SAVED_SEARCH ::= ( "-sa" (* hide = "true" *) | "--savedsearch" (* slogan = "Specify the savedsearch containing list of nodes. Tip: -sa for short" *) ) WS SAVED_SEARCH_NAME { "," (* slogan = "Comma used to add another savedsearch to the list." *) SAVED_SEARCH_NAME }

COLLECTION ::= ( "-co" (* hide = "true" *) | "--collection" (* slogan = "Specify the collection containing list of nodes. Tip: -co for short" *) ) WS COLLECTION_NAME { "," (* slogan = "Comma used to add another collection to the list." *) COLLECTION_NAME }

SAVED_SEARCH_NAME ::= <saved_search_name> (* slogan = "Scope the Command using savedsearch names. E.g. MySavedSearch1, MySavedSearch2" *) )

COLLECTION_NAME ::= <collection_name> (* slogan = "Scope the Command using collection names. E.g. MyCollection1, MyCollection2" *) )

TRUST_NODE_XML_FILE ::= WS ( "-xf" (* hide = "true" *) | "--xmlfile" (* slogan = "Specify the xml file. Tip: -xf for short" *) ) WS "file:" <file_name> (* inputType = "fileName" slogan = "Specify an xml file" *)

EXTERNAL_CA ::= WS ( "-ec" (* hide = "true" *) | "--extca" )
